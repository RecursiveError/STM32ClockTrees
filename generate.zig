const std = @import("std");

const ClockTree = @import("src/clocktree.zig");
const Tree = ClockTree.Tree;
const Element = ClockTree.Element;
const Sources = ClockTree.Sources;
const Output = ClockTree.Output;
const Input = ClockTree.Input;
const ReferenceValue = ClockTree.ReferenceValue;
const Reference = ClockTree.Reference;

var nameMap: std.StringHashMap(Element) = undefined;
var RefIDmap: std.StringHashMap([]const u8) = undefined;
var RefValIDmap: std.StringHashMap(f32) = undefined;

pub fn main() !void {
    var out_buffer: [50]u8 = undefined;
    var json_dir = try std.fs.cwd().openDir("out-json", .{ .iterate = true });
    var iter = json_dir.iterate();
    while (true) {
        const file = try iter.next();
        if (file) |entry| {
            switch (entry.kind) {
                .file => {
                    std.debug.print("create new file from: {s}\n", .{entry.name});
                    const name = entry.name;
                    const out_name = std.fmt.bufPrint(&out_buffer, "out-zig/{s}.zig", .{name[0..(name.len - 5)]}) catch continue;
                    const json_file = json_dir.openFile(name, .{}) catch continue;
                    create_files(json_file, out_name) catch |err| {
                        std.log.info("Fail to create {s} Error: {any}", .{ name, err });
                    };
                    json_file.close();
                },
                else => {},
            }
            continue;
        }
        break;
    }
}

fn load_global_maps(tree: *const Tree, alloc: std.mem.Allocator) !void {
    var nmap = std.StringHashMap(Element).init(alloc);
    var rmap = std.StringHashMap([]const u8).init(alloc);
    var vmap = std.StringHashMap(f32).init(alloc);
    for (tree.element) |element| {
        try nmap.put(element.name, element);
        try rmap.put(element.reference_id, element.name);
        for (element.reference) |ref| {
            load_reference(ref.value, &vmap);
        }
    }
    nameMap = nmap;
    RefIDmap = rmap;
    RefValIDmap = vmap;
}

fn load_reference(ref: ReferenceValue, map: *std.StringHashMap(f32)) void {
    switch (ref) {
        .NumberList => |list| {
            for (list.list, list.id_list) |val, name| {
                map.put(name, val) catch continue;
            }
        },
        else => {},
    }
}

fn create_files(json_file: std.fs.File, out_name: []const u8) !void {
    var arena_state = std.heap.ArenaAllocator.init(std.heap.page_allocator);
    defer arena_state.deinit();
    const arena = arena_state.allocator();
    const file_slice = json_file.readToEndAlloc(arena, 2042 * 1000) catch unreachable;

    const corrent_tree = try std.json.parseFromSlice(Tree, arena, file_slice, .{ .duplicate_field_behavior = .use_last });
    const tree = &corrent_tree.value;
    const clear_tree = try clean_tree(tree, arena);
    std.fs.cwd().deleteFile(out_name) catch {};
    const zig_file = try std.fs.cwd().createFile(out_name, .{ .truncate = false });
    errdefer zig_file.close();
    try load_global_maps(&clear_tree, arena);
    _ = try zig_file.write(
        \\const std = @import("std");
        \\const clock = @import("../ClockNode.zig");
        \\const ClockNode = clock.ClockNode;
        \\const ClockNodeTypes = clock.ClockNodesTypes;
        \\const ClockState = clock.ClockState;
        \\const ClockError = clock.ClockError;
        \\
        \\
    );
    try create_configs(&clear_tree, zig_file.writer().any());
    try create_conf_struct(&clear_tree, zig_file.writer().any());
    try create_alter_conf_struct(&clear_tree, zig_file.writer().any());
    try create_clock_struct(&clear_tree, zig_file.writer().any());
    zig_file.close();
    var ch = std.process.Child.init(&[_][]const u8{ "zig", "fmt", out_name }, arena);
    _ = try ch.spawnAndWait();
}

fn clean_tree(base_tree: *const Tree, alloc: std.mem.Allocator) !Tree {
    var new_elements = std.ArrayList(Element).init(alloc);
    for (base_tree.element) |element| {
        if (TypeMap.get(element.Elementtype)) |_| {
            switch (get_default_value(element.reference)) {
                .NoReference => continue,
                else => {},
            }
            try new_elements.append(element);
        }
    }

    return Tree{ .name = base_tree.name, .element = try new_elements.toOwnedSlice() };
}

fn get_default_value(references: []Reference) ReferenceValue {
    var val: ReferenceValue = undefined;
    for (references) |ref| {
        //only care abount the null expression
        if (ref.expr) |_| continue;
        //if the expr is resolved at the interface level, it has higher priority to be the default value
        if (ref.pre_evaluated) return ref.value;
        val = ref.value;
    }
    return val;
}

fn check_input(name: []const u8) bool {
    return if (nameMap.get(name)) |_| true else false;
}

//========= config types =========
///create enum configs for each type in clocktree
fn create_configs(tree: *const Tree, writer: std.io.AnyWriter) !void {
    const palloc = std.heap.page_allocator;
    var parentmap = std.StringHashMap(void).init(palloc);
    defer parentmap.deinit();

    for (tree.element) |element| {
        if (std.mem.eql(u8, "output", element.Elementtype)) {
            continue;
        } else if (std.mem.eql(u8, "activeOutput", element.Elementtype)) {
            continue;
        }

        if (parentmap.get(element.reference_id)) |_| continue;
        try parentmap.put(element.reference_id, {});

        switch (get_default_value(element.reference)) {
            .NumberList => |list| {
                try create_list_config(&element, list, writer);
            },
            .Input => |_| {
                try create_mutiplexor_config(&element, writer);
            },
            .Number,
            => |values| {
                try create_number_config(&element, values, writer);
            },
            else => {},
        }
    }
}

fn create_list_config(element: *const Element, list: ClockTree.ReferenceNumberList, writer: std.io.AnyWriter) !void {
    try writer.print(
        \\pub const @"{s}Conf" = enum{{
        \\
    , .{element.reference_id});
    for (list.id_list) |name| {
        try writer.print("@\"{s}\",\n", .{name});
    }

    try writer.print(
        \\pub fn get(self: @This()) f32 {{
        \\  return switch(self) {{
        \\
    , .{});

    for (list.list, list.id_list) |number, name| {
        try writer.print(".@\"{s}\" => {d},\n", .{ name, number });
    }

    try writer.print(
        \\}};
        \\}}
        \\}};
        \\
    , .{});
}

fn create_mutiplexor_config(element: *const Element, writer: std.io.AnyWriter) !void {
    try writer.print("pub const @\"{s}Conf\" = enum{{\n", .{element.reference_id});
    for (element.sources) |sources| {
        switch (sources) {
            .input => |input| {
                if (check_input(input.from)) {
                    if (input.RefValue) |val| {
                        try writer.print("{s},\n", .{val});
                    } else {
                        try writer.print("{s},\n", .{input.from});
                    }
                }
            },
            else => {},
        }
    }
    try writer.print("}};\n", .{});
}

fn create_number_config(element: *const Element, number: ClockTree.ReferenceNumber, writer: std.io.AnyWriter) !void {
    const name = element.reference_id;
    const max = number.max;
    const min = number.min;

    try writer.print(
        \\pub const @"{s}Conf" = enum(u32) {{
        \\    _,
        \\    pub fn get(num: @This()) f32 {{
        \\        const val: u32 = @intFromEnum(num);
        \\        return @as(f32,@floatFromInt(val));
        \\    }}
        \\
        \\   pub fn max() f32 {{
        \\      return {d};
        \\   }}
        \\
        \\   pub fn min() f32 {{
        \\      return {d};
        \\  }}
        \\}};
        \\
    , .{ name, max, min });
}

//======= config struct ==========
///cretate the config struct
fn create_conf_struct(tree: *const Tree, writer: std.io.AnyWriter) !void {
    try writer.print("pub const Config = struct {{\n", .{});
    for (tree.element) |element| {
        const name = element.name;
        const id_name = element.reference_id;
        if (std.mem.eql(u8, "output", element.Elementtype)) {
            continue;
        } else if (std.mem.eql(u8, "activeOutput", element.Elementtype)) {
            continue;
        }

        switch (get_default_value(element.reference)) {
            .Number => |number| {
                try writer.print("{s}: @\"{s}Conf\" = @enumFromInt({d}),\n", .{ name, id_name, number.default });
            },
            .NumberList => |list| {
                const def_val = blk: {
                    for (list.id_list, list.list) |val_name, number| {
                        if (number == list.default) break :blk val_name;
                    }
                    break :blk list.id_list[0];
                };
                try writer.print("{s}: @\"{s}Conf\" = ", .{ name, id_name });

                try writer.print(".@\"{s}\"", .{def_val});
                try writer.print(",\n", .{});
            },
            .Input => |input| {
                if (input.DefaultInput.len > 0) {
                    try writer.print("{s}: @\"{s}Conf\" = .{s},\n", .{ name, id_name, input.DefaultInput });
                } else {
                    for (element.sources) |src| {
                        switch (src) {
                            .input => |inpn| {
                                if (inpn.RefValue) |ref| {
                                    try writer.print("{s}: @\"{s}Conf\" = .{s},\n", .{ name, id_name, ref });
                                    break;
                                }
                            },
                            else => {},
                        }
                    }
                }
            },
            else => {},
        }
    }

    try writer.print("}};\n\n", .{});
}

fn create_alter_conf_struct(tree: *const Tree, writer: std.io.AnyWriter) !void {
    const palloc = std.heap.page_allocator;
    var parentmap = std.StringHashMap(void).init(palloc);
    defer parentmap.deinit();

    try writer.print("pub const ConfigWithRef = struct {{\n", .{});
    for (tree.element) |element| {
        const id_name = element.reference_id;
        if (std.mem.eql(u8, "output", element.Elementtype)) {
            continue;
        } else if (std.mem.eql(u8, "activeOutput", element.Elementtype)) {
            continue;
        }

        if (parentmap.get(id_name)) |_| continue;
        try parentmap.put(id_name, {});

        switch (get_default_value(element.reference)) {
            .Number => |number| {
                try writer.print("@\"{s}\": @\"{s}Conf\" = @enumFromInt({d}),\n", .{ id_name, id_name, number.default });
            },
            .NumberList => |list| {
                const def_val = blk: {
                    for (list.id_list, list.list) |val_name, number| {
                        if (number == list.default) break :blk val_name;
                    }
                    break :blk list.id_list[0];
                };
                try writer.print("@\"{s}\": @\"{s}Conf\" = ", .{ id_name, id_name });

                try writer.print(".@\"{s}\"", .{def_val});
                try writer.print(",\n", .{});
            },
            .Input => |input| {
                if (input.DefaultInput.len > 0) {
                    try writer.print("@\"{0s}\": @\"{0s}Conf\" = .{1s},\n", .{ id_name, input.DefaultInput });
                } else {
                    for (element.sources) |src| {
                        switch (src) {
                            .input => |inpn| {
                                if (inpn.RefValue) |ref| {
                                    try writer.print("@\"{0s}\": @\"{0s}Conf\" = .{1s},\n", .{ id_name, ref });
                                    break;
                                }
                            },
                            else => {},
                        }
                    }
                }
            },
            else => {},
        }
    }
    try create_into_config(tree, writer);

    try writer.print("}};\n\n", .{});
}

fn create_into_config(tree: *const Tree, writer: std.io.AnyWriter) !void {
    _ = try writer.write(
        \\pub fn into_config(self: *const ConfigWithRef) Config {
        \\ return .{
        \\
    );
    for (tree.element) |element| {
        const name = element.name;
        const id_name = element.reference_id;
        if (std.mem.eql(u8, "output", element.Elementtype)) {
            continue;
        } else if (std.mem.eql(u8, "activeOutput", element.Elementtype)) {
            continue;
        }
        switch (get_default_value(element.reference)) {
            .NoReference, .FixedNumber => continue,
            else => try writer.print(".{s} = self.@\"{s}\",\n", .{ name, id_name }),
        }
    }

    _ = try writer.write(
        \\  };
        \\}
        \\
    );
}

//======= Clock Struct ========

fn create_clock_struct(tree: *const Tree, writer: std.io.AnyWriter) !void {
    try writer.print(
        \\pub const ClockTree = struct {{
        \\  const this = @This();
        \\
        \\
    , .{});

    try create_clock_types(tree, writer);
    try create_init_comptime(tree, writer);
    try create_clock_validate(tree, writer);

    try writer.print(
        \\}};
        \\
        \\
    , .{});
}

const ValueTyRef = *const fn (*const Element, ReferenceValue, std.io.AnyWriter) anyerror!void;
const ValueTyMap = std.StaticStringMap(ValueTyRef).initComptime(.{
    .{ "ExtraRef", create_source_val },
    .{ "distinctValsSource", create_source_val },
    .{ "fixedSource", create_source_val },
    .{ "variedSource", create_source_val },
    .{ "fractional", create_source_val },
    .{ "multiplicator", create_prescaler_val },
    .{ "multiplicatorFrac", create_mulfrac_val },
    .{ "devisor", create_prescaler_val },
    .{ "divisor", create_prescaler_val },
    .{ "multiplexor", create_multiplex_val },
    .{ "xbar", create_multiplex_val },
    .{ "output", create_output_val },
    .{ "activeOutput", create_output_val },
});

const TypeRef = *const fn (*const Element, std.io.AnyWriter) anyerror!void;
const TypeMap = std.StaticStringMap(TypeRef).initComptime(.{
    .{ "ExtraRef", create_source },
    .{ "distinctValsSource", create_source },
    .{ "fixedSource", create_source },
    .{ "variedSource", create_source },
    .{ "fractional", create_source },
    .{ "multiplicator", create_prescaler },
    .{ "multiplicatorFrac", create_mulfrac },
    .{ "devisor", create_prescaler },
    .{ "divisor", create_prescaler },
    .{ "multiplexor", create_multiplex },
    .{ "xbar", create_multiplex },
    .{ "output", create_output },
    .{ "activeOutput", create_output },
});

fn create_clock_types(tree: *const Tree, writer: std.io.AnyWriter) !void {
    for (tree.element) |element| {
        if (std.mem.eql(u8, "ExtraRef", element.Elementtype)) {
            try writer.print("@\"{s}\": ClockNodeTypes,\n", .{element.name});
        } else {
            try writer.print("@\"{s}\": ClockNode,\n", .{element.name});
        }
    }
    _ = try writer.write("\n");
}

fn create_init_comptime(tree: *const Tree, writer: std.io.AnyWriter) !void {
    const palloc = std.heap.page_allocator;
    var parentmap = std.StringHashMap(bool).init(palloc);
    defer parentmap.deinit();
    try writer.print(
        \\pub fn init_comptime(comptime config: Config) this {{
        \\
    , .{});
    for (tree.element) |element| {
        if (parentmap.get(element.name)) |_| continue;
        try parentmap.put(element.name, true);
        try create_node_source(&element, writer, &parentmap);
    }

    _ = try writer.write("return .{\n");
    for (tree.element) |element| {
        if (std.mem.eql(u8, "ExtraRef", element.Elementtype)) {
            try writer.print(".@\"{0s}\" = @\"{0s}val\",\n", .{element.name});
        } else {
            try writer.print(".@\"{0s}\" = @\"{0s}\",\n", .{element.name});
        }
    }

    _ = try writer.write(
        \\  };
        \\}
        \\
    );
}

const NodeType = enum { none, simple, frac, multi };
const NodeMap = std.StaticStringMap(NodeType).initComptime(.{
    .{ "distinctValsSource", NodeType.none },
    .{ "fixedSource", NodeType.none },
    .{ "variedSource", NodeType.none },
    .{ "fractional", NodeType.none },
    .{ "multiplicator", NodeType.simple },
    .{ "multiplicatorFrac", NodeType.frac },
    .{ "devisor", NodeType.simple },
    .{ "divisor", NodeType.simple },
    .{ "multiplexor", NodeType.multi },
    .{ "xbar", NodeType.multi },
    .{ "output", NodeType.simple },
    .{ "activeOutput", NodeType.simple },
});
fn create_node_source(element: *const Element, writer: std.io.AnyWriter, parentmap: *std.StringHashMap(bool)) !void {
    //std.debug.print("create source for: {s}\n", .{element.name});
    const nodet: NodeType = NodeMap.get(element.Elementtype) orelse NodeType.none;

    switch (nodet) {
        .none => {},
        .simple => {
            const input_name = try find_fist_input(element.sources);
            if (!std.mem.eql(u8, input_name, element.name)) {
                if (parentmap.get(input_name)) |_| {} else {
                    if (nameMap.get(input_name)) |input_element| {
                        try parentmap.put(input_name, true);
                        try create_node_source(&input_element, writer, parentmap);
                    }
                }
            }
        },

        .frac => {
            const frac_inputs = try find_fract(element.sources);
            const names = &[_][]const u8{ frac_inputs.inputid, frac_inputs.fractid };
            for (names) |name| {
                if (!std.mem.eql(u8, name, element.name)) {
                    if (parentmap.get(name)) |_| {} else {
                        if (nameMap.get(name)) |input_element| {
                            try parentmap.put(name, true);
                            try create_node_source(&input_element, writer, parentmap);
                        }
                    }
                }
            }
        },

        .multi => {
            for (element.sources) |source| {
                switch (source) {
                    .input => |input| {
                        const input_name = input.from;
                        if (std.mem.eql(u8, input_name, element.name)) continue;
                        if (parentmap.get(input_name)) |_| continue;
                        const input_element = nameMap.get(input_name) orelse continue;
                        try parentmap.put(input_name, true);
                        try create_node_source(&input_element, writer, parentmap);
                    },
                    else => {},
                }
            }
        },
    }
    var max_refs: [25][]const u8 = undefined;
    for (element.reference) |ref| {
        if (ref.expr) |expr| {
            const to_create = get_expr_refs(expr, &max_refs);
            for (to_create) |name| {
                if (parentmap.get(name)) |_| continue;
                try parentmap.put(name, true);
                const input_element = nameMap.get(name) orelse continue;
                try create_node_source(&input_element, writer, parentmap);
            }
        }
    }
    try create_node(element, writer);
    //std.debug.print("===end create===\n", .{});
}

fn get_expr_refs(expr: []const u8, expr_deep: [][]const u8) []const []const u8 {
    var expr_idx: usize = 0;
    var expr_deep_idx: usize = 0;
    while (expr_idx < expr.len) {
        switch (expr[expr_idx]) {
            '!', '(', ')', '/', '=', '|', '&', ' ', '>', '<' => {
                expr_idx += 1;
            },
            else => {
                const str = get_cur_str(expr[expr_idx..]);
                expr_idx += str.len;
                if (is_element(str)) {
                    expr_deep[expr_deep_idx] = str;
                    expr_deep_idx += 1;
                }
            },
        }
    }

    return expr_deep[0..expr_deep_idx];
}

fn is_element(str: []const u8) bool {
    return if (nameMap.get(str)) |_| true else false;
}
fn create_node(element: *const Element, writer: std.io.AnyWriter) !void {
    try create_node_value(element, writer);
    if (std.mem.eql(u8, "ExtraRef", element.Elementtype)) return;
    try writer.print(
        \\const @"{0s}": ClockNode = .{{
        \\  .name = "{0s}",
        \\  .Nodetype = @"{0s}val",
        \\
    , .{element.name});
    const callback = TypeMap.get(element.Elementtype) orelse return error.InvalidType;
    callback(element, writer) catch |err| {
        switch (err) {
            error.RecursiveNode => {},
            else => return err,
        }
    };

    try writer.print(
        \\}};
        \\ 
    , .{});
}

fn create_node_value(element: *const Element, writer: std.io.AnyWriter) !void {
    const callback = ValueTyMap.get(element.Elementtype) orelse return error.InvalidType;
    const defualt = get_default_value(element.reference);
    var have_expr: bool = false;
    var buf: [500]u8 = undefined;
    try writer.print(
        \\const @"{s}val" =
    , .{element.name});
    for (element.reference) |reference| {
        const zig_expr = trasnlate_expression(reference, &buf) orelse continue;
        if (!have_expr) {
            try writer.print(
                \\blk: {{
                \\if({s}){{
                \\  break :blk 
            , .{zig_expr});

            callback(element, reference.value, writer) catch |err| {
                switch (err) {
                    error.RecursiveNode => {},
                    else => return err,
                }
            };

            try writer.print(
                \\}}
                \\
            , .{});
            have_expr = true;
        } else if (have_expr) {
            try writer.print(
                \\else if({s}){{
                \\  break :blk 
            , .{zig_expr});
            callback(element, reference.value, writer) catch |err| {
                switch (err) {
                    error.RecursiveNode => {},
                    else => return err,
                }
            };
            try writer.print(
                \\}}
                \\
            , .{});
        }
    }

    if (have_expr) {
        try writer.print(
            \\else {{
            \\  break :blk 
        , .{});
        callback(element, defualt, writer) catch |err| {
            switch (err) {
                error.RecursiveNode => {},
                else => return err,
            }
        };
        try writer.print(
            \\}}
            \\}}; 
            \\
        , .{});
    } else {
        callback(element, defualt, writer) catch |err| {
            switch (err) {
                error.RecursiveNode => {},
                else => return err,
            }
        };
    }
}

fn trasnlate_expression(ref: Reference, buf: []u8) ?[]const u8 {
    var inner: [550]u8 = undefined;
    var inner_idx: usize = 0;

    if (ref.expr) |expr| {
        const len = expr.len;
        var idx: usize = 0;
        while (idx < len) {
            switch (expr[idx]) {
                '!', '(', ')', '/', ' ', '<', '>' => {
                    inner[inner_idx] = expr[idx];
                    idx += 1;
                    inner_idx += 1;
                },

                '=' => {
                    std.mem.copyForwards(u8, inner[inner_idx..], "==");
                    idx += 1;
                    inner_idx += 2;
                },

                '|' => {
                    std.mem.copyForwards(u8, inner[inner_idx..], "or");
                    idx += 1;
                    inner_idx += 2;
                },

                '&' => {
                    std.mem.copyForwards(u8, inner[inner_idx..], "and");
                    idx += 1;
                    inner_idx += 3;
                },

                else => {
                    const str = get_cur_str(expr[idx..]);
                    const val = string_to_idval(str, inner[inner_idx..]) catch return null;
                    idx += str.len;
                    inner_idx += val;
                },
            }
        }
        std.mem.copyForwards(u8, buf, inner[0..inner_idx]);
        return buf[0..inner_idx];
    }

    return null;
}

fn get_cur_str(buf: []const u8) []const u8 {
    var index: usize = 0;
    for (buf) |ch| {
        if (ch == '.') {
            index += 1;
        } else if (ch == '_') {
            index += 1;
        } else if (std.ascii.isAlphanumeric(ch)) {
            index += 1;
        } else break;
    }
    return buf[0..index];
}

var last_node: ?Element = null;
fn string_to_idval(buf: []const u8, outbuf: []u8) !usize {
    if (RefIDmap.get(buf)) |data| {
        last_node = nameMap.get(data);
        return (try std.fmt.bufPrint(outbuf, "@\"{s}val\".num_val()", .{data})).len;
    }

    if (RefValIDmap.get(buf)) |data| {
        return (try std.fmt.bufPrint(outbuf, "{d}", .{data})).len;
    }

    if (nameMap.get(buf)) |_| {
        if (last_node) |node| {
            const size = (try std.fmt.bufPrint(outbuf, "@as(f32, @floatFromInt(@as(usize, @intFromEnum(@\"{s}Conf\".{s}))))", .{ node.name, buf })).len;
            last_node = null;
            return size;
        }
        return error.fail;
    }

    const is_number: ?f32 = std.fmt.parseFloat(f32, buf) catch null;
    if (is_number) |data| {
        return (try std.fmt.bufPrint(outbuf, "{d}", .{data})).len;
    }

    return error.fail;
}

fn find_fist_input(sources: []const ClockTree.Sources) ![]const u8 {
    for (sources) |source| {
        switch (source) {
            .input => |input| {
                return input.from;
            },
            else => {},
        }
    }

    return error.NoInputFind;
}

fn create_prescaler(element: *const Element, writer: std.io.AnyWriter) anyerror!void {
    const name = element.name;
    const first_input = try find_fist_input(element.sources);
    if (std.mem.eql(u8, name, first_input)) return error.RecursiveNode;
    try writer.print(
        \\.parents = &[_]*const ClockNode{{&@"{s}"}},
        \\
    , .{first_input});
}

const PLLMap = std.StaticStringMap([]const u8).initComptime(.{
    .{ "multiplicator", "MUL" },
    .{ "multiplicatorFrac", "MUL" },
    .{ "devisor", "DIV" },
    .{ "divisor", "DIV" },
});

fn create_prescaler_val(element: *const Element, value: ReferenceValue, writer: std.io.AnyWriter) anyerror!void {
    const name = element.name;
    const first_input = try find_fist_input(element.sources);
    if (std.mem.eql(u8, name, first_input)) return error.RecursiveNode;
    var buf: [5]u8 = undefined;
    const prefix = PLLMap.get(element.Elementtype) orelse return error.InvalidPrescalerType;
    const lower_prefix = to_lower(&buf, prefix);
    switch (value) {
        .Number => |limits| {
            try writer.print(
                \\ClockNodeTypes{{ .{s} = .{{ .value = config.@"{s}".get(), .limit = .{{ .max = {d}, .min = {d} }}, }}, }};
                \\
            , .{ lower_prefix, name, limits.max, limits.min });
        },
        .NumberList => {
            try writer.print(
                \\ClockNodeTypes{{ .{s} = .{{ .value = config.@"{s}".get() }} }};
                \\
            , .{ lower_prefix, name });
        },
        .FixedNumber => |num| {
            try writer.print(
                \\ClockNodeTypes{{ .{s} = .{{ .value = {d} }}, }};
                \\
            , .{ lower_prefix, num });
        },
        else => {
            return error.InvalidMulRef;
        },
    }
}

const FracIds = struct {
    inputid: []const u8,
    fractid: []const u8,
};

fn to_lower(out: []u8, from: []const u8) []const u8 {
    const size: usize = @min(out.len, from.len);
    for (0..size) |index| {
        out[index] = std.ascii.toLower(from[index]);
    }
    return out[0..size];
}

fn find_fract(sources: []Sources) !FracIds {
    var buffer: [50]u8 = undefined;
    var fract: ?[]const u8 = null;
    var input: ?[]const u8 = null;
    var default: [2]Input = undefined;
    var def_index: usize = 0;
    for (sources) |source| {
        switch (source) {
            .input => |from| {
                const id = to_lower(&buffer, from.from);
                default[def_index] = from;
                def_index += 1;
                if (std.mem.indexOf(u8, id, "frac")) |_| {
                    fract = from.from;
                    continue;
                }
                input = from.from;
                if (def_index == 2) break;
            },
            else => {},
        }
    }

    if (def_index > 2) return error.InvalidInput;

    if (fract) |fractional| {
        if (input) |from| {
            return FracIds{
                .fractid = fractional,
                .inputid = from,
            };
        }
    }

    return FracIds{
        .fractid = default[1].from,
        .inputid = default[0].from,
    };
}
fn create_mulfrac(element: *const Element, writer: std.io.AnyWriter) anyerror!void {
    const inputs = try find_fract(element.sources);
    try writer.print(
        \\.parents = &[_]*const ClockNode{{&@"{s}", &@"{s}"}},
        \\
    , .{ inputs.inputid, inputs.fractid });
}

fn create_mulfrac_val(element: *const Element, value: ReferenceValue, writer: std.io.AnyWriter) anyerror!void {
    const name = element.name;
    switch (value) {
        .Number => |limits| {
            try writer.print(
                \\ClockNodeTypes{{ .mulfrac = .{{ .value = config.@"{s}".get(), .limit = .{{ .max = {d}, .min = {d} }}, }}, }};
                \\
            , .{ name, limits.max, limits.min });
        },
        .NumberList => {
            try writer.print(
                \\ClockNodeTypes{{ .mulfrac = .{{ .value = config.@"{s}".get() }}, }};
                \\
            , .{name});
        },
        .FixedNumber => |num| {
            try writer.print(
                \\ClockNodeTypes{{ .mulfrac = .{{ .value = {d} }}, }};
                \\
            , .{num});
        },
        else => {
            return error.InvalidMulRef;
        },
    }
}

fn create_multiplex(element: *const Element, writer: std.io.AnyWriter) anyerror!void {
    try writer.print(
        \\
        \\.parents = &[_]*const ClockNode{{
    , .{});

    for (element.sources) |source| {
        switch (source) {
            .input => |input| {
                if (check_input(input.from)) {
                    try writer.print("&@\"{s}\",", .{input.from});
                }
            },
            else => {},
        }
    }
    _ = try writer.write(
        \\},
        \\
    );
}

fn create_multiplex_val(element: *const Element, _: ReferenceValue, writer: std.io.AnyWriter) anyerror!void {
    const name = element.name;

    try writer.print(
        \\ClockNodeTypes{{ .multi = @intFromEnum(config.{s}) }};
        \\
    , .{name});
}

fn create_output(element: *const Element, writer: std.io.AnyWriter) anyerror!void {
    const input_id = try find_fist_input(element.sources);
    if (std.mem.eql(u8, element.name, input_id)) return error.RecursiveNode;
    try writer.print(
        \\.parents = &[_]*const ClockNode{{&@"{s}"}},
        \\
    , .{input_id});
}

fn create_output_val(_: *const Element, value: ReferenceValue, writer: std.io.AnyWriter) anyerror!void {
    switch (value) {
        .Number => |num| {
            try writer.print(
                \\ClockNodeTypes{{ .output = .{{ .max = {d}, .min = {d} }}, }};
                \\
            , .{ num.max, num.min });
        },
        else => {
            try writer.print(
                \\ClockNodeTypes{{ .output = null }};
                \\
            , .{});
        },
    }
}

fn create_source(_: *const Element, _: std.io.AnyWriter) anyerror!void {}

fn create_source_val(element: *const Element, value: ReferenceValue, writer: std.io.AnyWriter) anyerror!void {
    const name = element.name;
    switch (value) {
        .Number => |limits| {
            try writer.print(
                \\ClockNodeTypes{{ .source = .{{ .value = config.@"{s}".get(), .limit = .{{ .max = {d}, .min = {d} }}, }}, }};
                \\
            , .{ name, limits.max, limits.min });
        },
        .NumberList => {
            try writer.print(
                \\ClockNodeTypes{{ .source = .{{ .value = config.@"{s}".get() }}, }};
                \\
            , .{name});
        },
        .FixedNumber => |num| {
            try writer.print(
                \\ClockNodeTypes{{ .source = .{{ .value = {d} }}, }};
                \\
            , .{num});
        },
        else => {},
    }
}

fn create_clock_validate(tree: *const Tree, writer: std.io.AnyWriter) !void {
    try writer.print(
        \\
        \\ pub fn validate(comptime self: *const this) void {{
        \\
    , .{});
    for (tree.element) |element| {
        const name = element.name;

        if (std.mem.eql(u8, element.Elementtype, "activeOutput")) {
            try writer.print(
                \\ _ = self.@"{s}".get_comptime();
                \\
            , .{name});
        }
    }

    try writer.print("\n}}\n", .{});
}
